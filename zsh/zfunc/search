# vim: set ft=zsh :

# Real-time ripgrep with fzf

if [[ $# == 0 ]]; then
  echo 'search: File type was not provided.'
  echo 'search: rg --type-list to see a list of supported file types.'
  return
fi

local filetype="$1"
local highlight_cmd="bat -l $filetype --color=always --pager=never {1} --highlight-line={2}"

if [[ $filetype == (js|ts)x ]]; then
  filetype="${filetype:0:-1}"
fi

local match=$(
  rg --type="$filetype" --color=never --line-number '.*' . |
  fzf --no-multi \
      --reverse \
      --delimiter : \
      --bind 'ctrl-t:toggle-preview' \
      --preview-window 'right,50%,border-left,+{2}+5/5,~5' \
      --preview "$highlight_cmd"
)

local lineno=$(echo "$match" | cut -d':' -f2)
local file=$(echo "$match" | cut -d':' -f1)

if [[ -n $file ]]; then
  $EDITOR $file +$lineno -c "exec 'normal zz'"
fi
